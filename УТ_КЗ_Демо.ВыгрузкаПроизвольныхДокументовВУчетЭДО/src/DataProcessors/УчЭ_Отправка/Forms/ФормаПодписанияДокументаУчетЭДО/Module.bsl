#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДанныеДляПодписания = Параметры.XMLДокумент;
	Если ЗначениеЗаполнено(Параметры.МножественноеПодписание) Тогда
		МножественноеПодписание = Параметры.МножественноеПодписание;
		ПутьДоКлюча = Параметры.ПутьДоКлюча;
		ПарольКлюча = Параметры.ПарольКлюча;
	Иначе
		МножественноеПодписание = Ложь;
	КонецЕсли;

	Разделитель = ?(ЗначениеЗаполнено(Параметры.Разделитель), Параметры.Разделитель, "%%SEP%%");

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ТекстДокумента = ПриОткрытииНаСервере();
	HTMLДокумент = ТекстДокумента;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура HTMLДокументДокументСформирован(Элемент)

	Элементы.HTMLДокумент.Документ.defaultView.xmlData = ДанныеДляПодписания;
	Элементы.HTMLДокумент.Документ.defaultView.SEPARATOR = Разделитель;

	Если МножественноеПодписание Тогда
		Элементы.HTMLДокумент.Документ.defaultView.keyFile = ПутьДоКлюча;
		Элементы.HTMLДокумент.Документ.defaultView.keyPass = ПарольКлюча;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура HTMLДокументПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)

	ПодписанныйТекст = Элементы.HTMLДокумент.Документ.getElementById("signedData").value;
	ПодписанныеДанные = Новый Структура;
	ПодписанныеДанные.Вставить("ПодписанныйТекст", ПодписанныйТекст);
	ПодписанныеДанные.Вставить("МассивДокументов", ЭтотОбъект.Параметры.МассивДокументов);
	Закрыть(ПодписанныеДанные);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПриОткрытииНаСервере()

	Если МножественноеПодписание Тогда
		Макет = Обработки.УчЭ_Отправка.ПолучитьМакет("МножественноеПодписаниеУчетЭДО").ПолучитьТекст();
	Иначе
		Макет = Обработки.УчЭ_Отправка.ПолучитьМакет("ОдиночноеПодписаниеУчетЭДО").ПолучитьТекст();
	КонецЕсли;

	Возврат Макет;

КонецФункции

#КонецОбласти
