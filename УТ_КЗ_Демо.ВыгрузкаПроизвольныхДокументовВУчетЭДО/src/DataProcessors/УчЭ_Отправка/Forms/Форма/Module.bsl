#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	НастроитьЭлементыФормы();
	УстановитьУсловноеОформление();
	Период.Вариант = ВариантСтандартногоПериода.ЭтотМесяц;
	УстановитьОтборДинамическихСписков();

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Организация = Настройки.Получить("Организация");

	Если ЗначениеЗаполнено(Организация) Тогда
		УстановитьОтборДинамическихСписков();
		ПолучитьНастройкиПодключения();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	//Обработчик = Новый ОписаниеОповещения("ОбработатьРезультатОтправки", ЭтотОбъект);
	//УведомленияКлиента.ПодключитьОбработчик("ОтправленыДокументыВУчетЭДО", Обработчик);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	//УведомленияКлиента.ОтключитьОбработчик("ОтправленыДокументыВУчетЭДО");

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ УчетЭДОУправлениеСвойствами.СоздатьДополнительныеСведения() Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(Нстр("ru = 'Не удалось создать дополнительные сведения!'"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	УстановитьОтборДинамическихСписков();

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	УстановитьОтборДинамическихСписков();

КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)

	УстановитьОтборДинамическихСписков();

КонецПроцедуры

&НаКлиенте
Асинх Процедура КаталогВыгрузкиФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите каталог сохранения файлов'");

	МассивФайлов = Ждать ДиалогОткрытияФайла.ВыбратьАсинх();
	Если МассивФайлов <> Неопределено Тогда
		КаталогВыгрузкиФайлов = МассивФайлов[0];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияДляАвторизацииПриИзменении(Элемент)

	ПолучитьНастройкиПодключения();

КонецПроцедуры

&НаКлиенте
Процедура ПутьДоЭЦПНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)

	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = НСтр("ru = 'Ключ для подписи'") + "(*.p12)|*.p12";
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите файл";
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		ПутьДоЭЦП = ДиалогОткрытияФайла.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройки(Команда)

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	// проверить создать доп. сведения
	

	Настройки = УчетЭДОВызовСервера.ИнициализироватьНастройкиПодключения();
	ЗаполнитьЗначенияСвойств(Настройки, ЭтотОбъект);
	УчетЭДОВызовСервера.СохранитьНастройкиПодключения(Настройки, Строка(Организация));

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	Настройки = УчетЭДОВызовСервера.ИнициализироватьНастройкиПодключения();
	ЗаполнитьЗначенияСвойств(Настройки, ЭтотОбъект);
	УчетЭДОВызовСервера.СохранитьНастройкиПодключения(Настройки, Строка(Организация));

	Если УчетЭДОВызовСервера.ПроверитьПодключение(Строка(Организация)) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Подключение установлено'"), ПолучитьНавигационнуюСсылку(ЭтотОбъект), НСтр("ru = 'Авторизационные данные подтверждены'"));
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(Нстр("ru = 'Ошибка авторизации!'"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЭЦП(Команда)

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	Настройки = УчетЭДОВызовСервера.ИнициализироватьНастройкиПодключения();
	ЗаполнитьЗначенияСвойств(Настройки, ЭтотОбъект);
	УчетЭДОВызовСервера.СохранитьНастройкиПодключения(Настройки, Строка(Организация));

	ОповещениеПриОкончанииПодписания = Новый ОписаниеОповещения("ПриОкончанииПодписанияПроверка", ЭтотОбъект);
	МассивСДокументами = Новый Массив;
	СтруктураДокумента = Новый Структура;
	СтруктураДокумента.Вставить("хэш", "<root><md5-hash>test</md5-hash></root>");
	МассивСДокументами.Добавить(СтруктураДокумента);
	Файл = Новый Файл(Настройки.ПутьДоЭЦП);
	Если Не Файл.Существует() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Файла ЭЦП по указанному пути не обнаружено'"));
	Иначе
		УчетЭДОКлиент.ПодписатьМассивXML(МассивСДокументами, ЭтотОбъект, ПутьДоЭЦП, ПарольОтЭЦП, ОповещениеПриОкончанииПодписания);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)

	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выделенных строк, отправка документов в Учет.ЭДО не может быть выполнена.'"));
		Возврат;
	КонецЕсли;

	ВыбранныеДокументы = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
		ВыбранныеДокументы.Добавить(ДанныеСтроки.Ссылка);
	КонецЦикла;

	ПараметрыОтправки = УчетЭДОВызовСервера.ПараметрыОтправкиВУчетЭДО();
	ПараметрыОтправки.ОтправкаПриАкцепте = Ложь;
	РезультатПодготовки = УчетЭДОВызовСервера.РезультатПодготовкиОтправкиДокументов(ВыбранныеДокументы, ПараметрыОтправки);
	Если РезультатПодготовки.НеотправляемыеДокументы.Количество() > 0 Тогда
		Для Каждого ДанныеНеотправляемогоДокумента Из РезультатПодготовки.НеотправляемыеДокументы Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ %1 не отправлен. %2.'"), ДанныеНеотправляемогоДокумента.Документ, ДанныеНеотправляемогоДокумента.Причина);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ДанныеНеотправляемогоДокумента.Документ);
		КонецЦикла;
	КонецЕсли;

	Если РезультатПодготовки.ОтправляемыеДокументы.Количество() > 0 Тогда
		Отказ = Ложь;
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		УчетЭДОВызовСервера.СформироватьПроизвольныеДокументы(РезультатПодготовки.ОтправляемыеДокументы, АдресХранилища, Отказ);
		УчетЭДОКлиент.ПодписатьИОтправитьПроизвольныеДокументы(РезультатПодготовки.ОтправляемыеДокументы, АдресХранилища, Строка(Организация), Отказ);
	Иначе
		ЗаголовокОповещения = НСтр("ru = 'Учет.ЭДО'");
		ТекстОповещения = НСтр("ru = 'Отправка документов в Учет.ЭДО завершена неудачно.'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусы(Команда)

	ОчиститьСообщения();
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выделенных строк, обновление статусов не может быть выполнено.'"));
		Возврат;
	КонецЕсли;

	ВыбранныеДокументы = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
		ДанныДокумента = Новый Структура("Организация, Документ, ИдентификаторВЭДО", Строка(Организация), ДанныеСтроки.Ссылка, ДанныеСтроки.ИдентификаторВЭДО);
		ВыбранныеДокументы.Добавить(ДанныДокумента);
	КонецЦикла;

	Отказ = Ложь;
	УчетЭДОВызовСервера.ОбновитьСтатусыДокументов(ВыбранныеДокументы, Отказ);

	Если Отказ Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Нстр("ru = 'Не удалось обновить статусы документов!'"));
	Иначе
		Элементы.Список.Обновить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНаСайте(Команда)

	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Отказ = Ложь;

	ДанныеДокумента = Новый Структура("Организация, Документ, ИдентификаторДокумента", Строка(Организация), ТекущаяСтрока.Ссылка, ТекущаяСтрока.ИдентификаторВЭДО);

	HttpСсылкаНаДокумент = УчетЭДОВызовСервера.ПолучитьHTTPСсылкуНаДокумент(ДанныеДокумента, Отказ);
	Если Отказ Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Нстр("ru = 'Не удалось получить ссылку документа!'"));
	Иначе
		ПерейтиПоНавигационнойСсылке(HttpСсылкаНаДокумент);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыФормы()

	Если Параметры.ТекущаяСтраница <> "" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы[Параметры.ТекущаяСтраница];
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДинамическихСписков()

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Дата", Период.ДатаНачала, ВидСравненияКомпоновкиДанных.БольшеИлиРавно, , ЗначениеЗаполнено(Период));
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Дата", КонецДня(Период.ДатаОкончания),	ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, , ЗначениеЗаполнено(Период));

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно, , ЗначениеЗаполнено(Организация));
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Контрагент", Контрагент, ВидСравненияКомпоновкиДанных.Равно, , ЗначениеЗаполнено(Контрагент));

КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройкиПодключения()

	Настройки = УчетЭДОВызовСервера.ПолучитьНастройкиПодключения(Организация);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("Список");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СтатусЭДО");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.НейтральноСиний);

КонецПроцедуры

&НаКлиенте
Процедура ПриОкончанииПодписанияПроверка(Результат, Параметры) Экспорт

	Разделитель = "%%SEP%%";
	Если Результат <> Неопределено Тогда
		МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Результат.ПодписанныйТекст, Разделитель);
		Если СтрНайти(МассивСтрок[0], "xml") <> -1 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Проверка завершена'"), ПолучитьНавигационнуюСсылку(ЭтотОбъект),
				НСтр("ru = 'Путь и пароль к ЭЦП введены корректно'"));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


//&НаКлиенте
//Процедура ОбработатьРезультатОтправки(ДанныеСообщения, ДополнительныеПарамеры = Неопределено) Экспорт
//
//	Если ДанныеСообщения = Неопределено Тогда
//		Возврат;
//	КонецЕсли;
//
//	ОповеститьОбИзменении(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
//
//КонецПроцедуры

#КонецОбласти
