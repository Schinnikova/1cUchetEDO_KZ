#Область ПрограммныйИнтерфейс

// Подписать массив XML.
//
// Параметры:
//	МассивДокументов - Массив из ДокументСсылка.РеализацияТоваровУслуг - документы для подписания
//	ФормаВладелец - Форма - форма владелец
//	ПутьДоКлючевогоКонтейнера - Строка - Путь до ключевого контейнера
//	ПарольКлючевогоКонтейнера - Строка - Пароль ключевого контейнера
//	ПриОкончанииПодписания	- ОписаниеОповещения - процедура
//	Разделитель - Строка - Разделитель
Процедура ПодписатьМассивXML(МассивДокументов, ФормаВладелец, ПутьДоКлючевогоКонтейнера, ПарольКлючевогоКонтейнера, ПриОкончанииПодписания, Разделитель = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(Разделитель) Тогда
		Разделитель = "%%SEP%%";
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	МассивXML = Новый Массив;
	Для Каждого Элемент Из МассивДокументов Цикл
		МассивXML.Добавить(Элемент["Хэш"]);
	КонецЦикла;
	ПараметрыФормы.Вставить("XMLДокумент", СтрСоединить(МассивXML, Разделитель));
	ПараметрыФормы.Вставить("МассивДокументов", МассивДокументов);
	ПараметрыФормы.Вставить("Разделитель", Разделитель);
	ПараметрыФормы.Вставить("МножественноеПодписание", Истина);
	ПараметрыФормы.Вставить("ПутьДоКлюча", ПутьДоКлючевогоКонтейнера);
	ПараметрыФормы.Вставить("ПарольКлюча", ПарольКлючевогоКонтейнера);

	ОткрытьФорму("Обработка.УчЭ_Отправка.Форма.ФормаПодписанияДокументаУчетЭДО", ПараметрыФормы, ФормаВладелец, , , , ПриОкончанииПодписания, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// Подписать и отправить произвольные документы.
//
// Параметры:
//	ОтправляемыеДокументы - Массив из Структура
//	АдресХранилища - УникальныйИдентификатор - Адрес во временном хранилище
//	Организация - СправочникСсылка.Организации - для получения настроек
//	Отказ - Булево
//	ОповещениеПоЗавершении - ОписаниеОповещения - Оповещение при завершении отправки
Процедура ПодписатьИОтправитьПроизвольныеДокументы(ОтправляемыеДокументы, АдресХранилища, Организация, Отказ, ОповещениеПоЗавершении = Неопределено) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	МассивОтправляемыхДокументов = ПолучитьИзВременногоХранилища(АдресХранилища);
	Настройки = УчетЭДОВызовСервера.ПолучитьНастройкиПодключения(Организация);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("АдресХранилища", АдресХранилища);
	ДополнительныеПараметры.Вставить("Организация", Строка(Организация));
	ДополнительныеПараметры.Вставить("ОповещениеПоЗавершении", ОповещениеПоЗавершении);
	ОповещениеПриОкончанииПодписания = Новый ОписаниеОповещения("ПриОкончанииПодписания", ЭтотОбъект, ДополнительныеПараметры);
	ПодписатьМассивXML(МассивОтправляемыхДокументов, ЭтотОбъект, Настройки.ПутьДоЭЦП, Настройки.ПарольОтЭЦП, ОповещениеПриОкончанииПодписания);

КонецПроцедуры     

// Подписывает и отправляет отзываемые документы.
//
// Параметры:
//   РезультатПодготовки - Структура - Документы, подлежащие отзыву
//   АдресХранилища - Строка - Адрес временного хранилища с отправляемыми документами
//   Организация - СправочникСсылка.Организации - Организация, от имени которой выполняется отправка
//   Отказ - Булево - Флаг отказа от выполнения операции
//   ОповещениеПоЗавершении - ОписаниеОповещения - Оповещение при завершении отзыва
Процедура ПодписатьИОтправитьОтзываемыеДокументы(РезультатПодготовки, АдресХранилища, Организация, Отказ, ОповещениеПоЗавершении = Неопределено) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	МассивОтзываемыхДокументов = ПолучитьИзВременногоХранилища(АдресХранилища);
	Настройки = УчетЭДОВызовСервера.ПолучитьНастройкиПодключения(Организация);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("РезультатПодготовки", РезультатПодготовки);
	ДополнительныеПараметры.Вставить("АдресХранилища", АдресХранилища);
	ДополнительныеПараметры.Вставить("Организация", Строка(Организация));
	ДополнительныеПараметры.Вставить("Отказ", Отказ);
	ДополнительныеПараметры.Вставить("ОповещениеПоЗавершении", ОповещениеПоЗавершении);
	ОповещениеПриОкончанииПодписания = Новый ОписаниеОповещения("ПриОкончанииПодписанияОтзываемыхДокументов", ЭтотОбъект, ДополнительныеПараметры);
	ПодписатьМассивXML(МассивОтзываемыхДокументов, ЭтотОбъект, Настройки.ПутьДоЭЦП, Настройки.ПарольОтЭЦП, ОповещениеПриОкончанииПодписания);

КонецПроцедуры

// При окончании подписания.
//
// Параметры:
//	Результат - Структура
//	ДополнительныеПараметры - Структура
Процедура ПриОкончанииПодписания(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось подписать и отправить документы!'"));
		Если ДополнительныеПараметры.Свойство("ОповещениеПоЗавершении") И ДополнительныеПараметры.ОповещениеПоЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПоЗавершении, Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Разделитель = "%%SEP%%";
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Результат.ПодписанныйТекст, Разделитель);
	Для Каждого Строка Из Результат.МассивДокументов Цикл
		Для Каждого Стр Из МассивСтрок Цикл
				// BSLLS:MagicNumber-off
			Если СтрНайти(Стр, Сред(Строка.Хэш, 17, 32)) <> 0 Тогда
				// BSLLS:LineLength-on
				Строка.Вставить("xml", Стр);
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		Строка.Вставить("ОтправкаПроизвольногоДокумента", Истина);
	КонецЦикла;

	МассивОтветов = УчетЭДОВызовСервера.ОтправитьПроизвольныеДокументы(Результат.МассивДокументов, ДополнительныеПараметры);
	
	Если ДополнительныеПараметры.Свойство("ОповещениеПоЗавершении") И ДополнительныеПараметры.ОповещениеПоЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПоЗавершении, МассивОтветов);
	КонецЕсли;

КонецПроцедуры    

// При окончании подписания отзываемых документов.
//
// Параметры:
//	Результат - Структура
//	ДополнительныеПараметры - Структура
Процедура ПриОкончанииПодписанияОтзываемыхДокументов(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось подписать и отправить отзыв документов!'"));
		Если ДополнительныеПараметры.Свойство("ОповещениеПоЗавершении") И ДополнительныеПараметры.ОповещениеПоЗавершении <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПоЗавершении, Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Разделитель = "%%SEP%%";
	НачалоПодписи = 17;
	КонецПодписи = 32;
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Результат.ПодписанныйТекст, Разделитель);
	Для Каждого Строка Из Результат.МассивДокументов Цикл
		Для Каждого Стр Из МассивСтрок Цикл
			Если СтрНайти(Стр, Сред(Строка.Хэш, НачалоПодписи, КонецПодписи)) <> 0 Тогда
				Строка.Вставить("xml", Стр);
				Продолжить;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Отказ = ДополнительныеПараметры.Отказ;
	УчетЭДОВызовСервера.ОтозватьДокументы(Результат.МассивДокументов, Отказ);
	
	// Формируем результат для оповещения
	МассивОтветов = Новый Массив;
	Для Каждого ДанныеДокумента Из Результат.МассивДокументов Цикл
		РезультатОтзыва = Новый Структура;
		РезультатОтзыва.Вставить("Документ", ДанныеДокумента.Документ);
		РезультатОтзыва.Вставить("Успех", НЕ Отказ);
		РезультатОтзыва.Вставить("Отозван", Истина);
		МассивОтветов.Добавить(РезультатОтзыва);
	КонецЦикла;
	
	Если ДополнительныеПараметры.Свойство("ОповещениеПоЗавершении") И ДополнительныеПараметры.ОповещениеПоЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПоЗавершении, МассивОтветов);
	КонецЕсли;

КонецПроцедуры

// Подписывает и отправляет в учет электронные документы по массиву реализаций.
//
// Параметры:
//	МассивРеализаций - Массив из ДокументСсылка.РеализацияТоваровУслуг - массив ссылок на документы реализации товаров и услуг.
//	ПараметрыОтправки - Структура - параметры отправки электронных документов.
Процедура ПодписатьИОтправитьВУчетЭДо(МассивРеализаций, ПараметрыОтправки = Неопределено) Экспорт
	
	Отказ = Ложь;
	РезультатПодготовки = УчетЭДОВызовСервера.РезультатПодготовкиОтправкиДокументов(МассивРеализаций, ПараметрыОтправки);
	Если РезультатПодготовки.ОтзываемыеДокументы.Количество() > 0 Тогда
		Организация = ОбщегоНазначенияКлиентСервер.ПервыйВКоллекции(РезультатПодготовки.ОтправляемыеДокументы).Организация;
		Если ПроверитьНастройкиОтправкиВУчетЭДО(Организация) Тогда
			АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, ПараметрыОтправки.УникальныйИдентификатор);
			УчетЭДОВызовСервера.СформироватьОтзываемыеДокументы(РезультатПодготовки.ОтзываемыеДокументы, АдресХранилища, Отказ);
			ПодписатьИОтправитьОтзываемыеДокументы(РезультатПодготовки, АдресХранилища, Строка(Организация), Отказ);
		КонецЕсли;
	ИначеЕсли РезультатПодготовки.ОтправляемыеДокументы.Количество() > 0 Тогда
		Организация = УчетЭДООбщегоНазначенияКлиентСервер.ПервыйВКоллекции(РезультатПодготовки.ОтправляемыеДокументы).Организация;
		Если ПроверитьНастройкиОтправкиВУчетЭДО(Организация) Тогда
			АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, ПараметрыОтправки.УникальныйИдентификатор);
			УчетЭДОВызовСервера.СформироватьПроизвольныеДокументы(РезультатПодготовки.ОтправляемыеДокументы, АдресХранилища, Отказ);
			ПодписатьИОтправитьПроизвольныеДокументы(РезультатПодготовки.ОтправляемыеДокументы, АдресХранилища, Строка(Организация), Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИфункции

Функция ПроверитьНастройкиОтправкиВУчетЭДО(Организация)

	Настройки = УчетЭДОВызовСервера.ПолучитьНастройкиПодключения(Организация);
	ПроверяемыеПоля = "Логин, Пароль, ПутьДоЭЦП, ПарольОтЭЦП";
	Для Каждого Свойство Из СтрРазделить(ПроверяемыеПоля, ", ", Ложь) Цикл
		Если Настройки[Свойство] = "" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не заполнены настройки подключения к Учет.ЭДО!'"));
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;

	Файл = Новый Файл(Настройки.ПутьДоЭЦП);
	Если НЕ Файл.Существует() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не найден файл ЭЦП!'"));
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;

КонецФункции

#КонецОбласти
