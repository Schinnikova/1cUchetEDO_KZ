#Область ПрограммныйИнтерфейс

// Настройки авторизации.
//
// Возвращаемое значение:
//	Структура - Настройки подключения к сервису:
// * Сервер - Строка - Сервер ЭДО.Учет https://edo.uchet.kz/
// * Порт - Строка
// * ЗащищенноеСоединение - Строка
// * ПроксиСервер - Строка
// * ПортПроксиСервера - Строка
// * Логин - Строка - логин пользователя в ЭДО.Учет
// * Пароль - Строка - пароль пользователя в ЭДО.Учет
// * ПутьДоЭЦП - Строка - путь до ЭЦП
// * ПарольОтЭЦП - Строка - пароль от ЭЦП
Функция ИнициализироватьНастройкиПодключения() Экспорт

	Настройки = Новый Структура;
	
	Настройки.Вставить("Сервер", "");
	Настройки.Вставить("Порт", "");
	Настройки.Вставить("ЗащищенноеСоединение", Ложь);
	Настройки.Вставить("ПроксиСервер", "");
	Настройки.Вставить("ПортПроксиСервера", ""); 
	Настройки.Вставить("ЛогинПрокси", "");
	Настройки.Вставить("ПарольПрокси", "");
	
	Настройки.Вставить("Логин", "");
	Настройки.Вставить("Пароль", "");
	Настройки.Вставить("ПутьДоЭЦП", "");
	Настройки.Вставить("ПарольОтЭЦП", "");

	Возврат Настройки;

КонецФункции

// Возвращает настройки подключения
//
// Параметры:
//	Организация - Строка - Наименование организации
//
// Возвращаемое значение:
//	Структура - см. НастройкиАвторизации()
Функция ПолучитьНастройкиПодключения(Организация) Экспорт

	КлючОбъекта = "ПараметрыАвторизацииЭДОУчет";
	КлючНастроек = Организация;

	СтруктураНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНастроек, ИнициализироватьНастройкиПодключения());
	СтруктураНастроек.Вставить("АдресСервера", АдресСервера(СтруктураНастроек));
	СтруктураНастроек.Вставить("Прокси", ПроксиПодключения(СтруктураНастроек));

	Возврат СтруктураНастроек;

КонецФункции

// Подготавливает настройки подключения для проверки.
//
// Параметры:
//	Настройки - Структура - Настройки подключения из формы
//
// Возвращаемое значение:
//	Структура - Подготовленные настройки с адресом сервера и прокси
Функция ПодготовитьНастройкиПодключения(Настройки) Экспорт

	ПодготовленныеНастройки = Настройки;
	ПодготовленныеНастройки.Вставить("АдресСервера", АдресСервера(Настройки));
	ПодготовленныеНастройки.Вставить("Прокси", ПроксиПодключения(Настройки));

	Возврат ПодготовленныеНастройки;

КонецФункции

// Сохраняет настройку авторизации в хранилище общих настроек.
//
// Параметры:
//	Настройки - Структура - см. НастройкиАвторизации()
//	Организация - Строка - Наименование организации
Процедура СохранитьНастройкиПодключения(Настройки, Организация) Экспорт

	КлючОбъекта = "ПараметрыАвторизацииЭДОУчет";
	КлючНастроек = Организация;
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, Настройки);

КонецПроцедуры

// Возвращает структуру параметров отправки документов в учет ЭДО
//
// Возвращаемое значение:
//	Структура - структура параметров отправки документов в учет ЭДО, содержащая следующие ключи:
//		* РазрешенаПовторнаяОтправка - Булево - флаг повторной отправки
//		* УникальныйИдентификатор - УникальныйИдентификатор - уникальный идентификатор формы
Функция ПараметрыОтправкиВУчетЭДО() Экспорт

	Структура = Новый Структура;
	Структура.Вставить("РазрешенаПовторнаяОтправка", Ложь);
	Структура.Вставить("УникальныйИдентификатор", Новый УникальныйИдентификатор);

	Возврат Структура;

КонецФункции

// Получить авторизационные данные.
//
// Параметры:
//	Настройки - Структура - см. НастройкиАвторизации()
//
// Возвращаемое значение:
//	См. КоннекторHTTP.ВызватьМетод
Функция ПолучитьАвторизационныеДанные(Настройки) Экспорт

	Данные = Новый Структура;

	Данные.Вставить("username", Настройки.Логин);
	Данные.Вставить("password", Настройки.Пароль);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Прокси", Настройки.Прокси);
	АдресРесурса = "/api/v2/login/access-token";
	Ответ = КоннекторHTTP.Post(Настройки.АдресСервера + АдресРесурса, Данные, ДополнительныеПараметры);
	Результат = КоннекторHTTP.КакJson(Ответ, Новый Структура("ПрочитатьВСоответствие", Ложь));

	Возврат Результат;

КонецФункции

// Получить авторизационные данные.
//
// Параметры:
//	Организация - Строка - Наименование организации
//
// Возвращаемое значение:
//	Булево, Структура - См. КоннекторHTTP.ВызватьМетод
Функция ПроверитьПодключение(Организация) Экспорт

	Настройки = ПолучитьНастройкиПодключения(Организация);
	Возврат ПроверитьПодключениеСНастройками(Настройки);

КонецФункции

// Проверить подключение с переданными настройками.
//
// Параметры:
//	Настройки - Структура - Настройки подключения к сервису
//
// Возвращаемое значение:
//	Булево - Результат проверки подключения
Функция ПроверитьПодключениеСНастройками(Настройки) Экспорт

	АвторизационныеДанные = ПолучитьАвторизационныеДанные(Настройки);

	Если АвторизационныеДанные.Свойство("detail") Тогда
		ОбщегоНазначения.СообщитьПользователю(АвторизационныеДанные.detail);
		Возврат Ложь;
	КонецЕсли;

	АдресРесурса = "/api/v2/users/me";
	Аутентификация = Новый Структура("Токен, Тип", АвторизационныеДанные.access_token, "Bearer");
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Прокси", Настройки.Прокси);
	ДополнительныеПараметры.Вставить("Аутентификация", Аутентификация);

	Ответ = КоннекторHTTP.Get(Настройки.АдресСервера + АдресРесурса, Неопределено, ДополнительныеПараметры);

	Возврат Ответ.КодСостояния = КодыСостоянияОтветаHTTPКлиентСервер.OK_200();

КонецФункции

// Сформировать произвольные документы.
//
// Параметры:
//	ОтправляемыеДокументы - Массив из Структура - Отправляемые документы
//	АдресХранилища - УникальныйИдентификатор -	адрес во временном хранилище
//	Отказ - Булево - Признак отказа
Процедура СформироватьПроизвольныеДокументы(ОтправляемыеДокументы, АдресХранилища, Отказ) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	МассивОтправляемыхДокументов = Новый Массив;

	ТаблицаПечати = ТаблицаПечати();

	Для Каждого ДанныеДокумента Из ОтправляемыеДокументы Цикл
		Для Каждого СтрокаПечати Из ТаблицаПечати Цикл
			ПечатныеФормы = УправлениеПечатью.СформироватьПечатныеФормы(СтрокаПечати.МенеджерПечати, СтрокаПечати.Макет,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеДокумента.Документ), Новый Структура("ВыводитьУслуги, НеВыводитьСообщения", Ложь, Истина));
			Для Каждого ПечатнаяФорма Из ПечатныеФормы.КоллекцияПечатныхФорм Цикл
				Если ПечатнаяФорма = Неопределено Или ПечатнаяФорма.ТабличныйДокумент.ВысотаТаблицы = 0 Тогда
					Продолжить;
				КонецЕсли;
				ПутьДоФайла = ПолучитьИмяВременногоФайла("pdf");
				ТабличныйДокумент = ПечатнаяФорма.ТабличныйДокумент;
				ТабличныйДокумент.Записать(ПутьДоФайла, ТипФайлаТабличногоДокумента.PDF);
				ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ПутьДоФайла);
				УдалитьФайлы(ПутьДоФайла);

				ХэшПечатнойФормы = ХешСуммаДвоичныхДанных(ДвоичныеДанныеФайла, , Истина);
				ФайлBase64 = Base64Строка(ДвоичныеДанныеФайла);

				ДанныеДляОтправки = Новый Структура;
				ДанныеДляОтправки.Вставить("Документ", ДанныеДокумента.Документ);
				ДанныеДляОтправки.Вставить("Хэш", "<root><md5-hash>" + ХэшПечатнойФормы + "</md5-hash></root>");
				ДанныеДляОтправки.Вставить("ФайлBase64", файлBase64);
				МассивОтправляемыхДокументов.Добавить(ДанныеДляОтправки);

			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	Если МассивОтправляемыхДокументов.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ПоместитьВоВременноеХранилище(МассивОтправляемыхДокументов, АдресХранилища);

КонецПроцедуры

// Отправить произвольные документы.
//
// Параметры:
//	МассивДокументов - Массив из Структура - Отправляемые документы
//	ДополнительныеПараметры - Структура - дополнитлеьные параметры
//
// Возвращаемое значение:
//	Массив из Структура - Массив ответов от сервера
Функция ОтправитьПроизвольныеДокументы(МассивДокументов, ДополнительныеПараметры) Экспорт

	МассивОтветов = Новый Массив;

	Настройки = ПолучитьНастройкиПодключения(ДополнительныеПараметры.Организация);
	АвторизационныеДанные = ПолучитьАвторизационныеДанные(Настройки);
	Если АвторизационныеДанные.Свойство("detail") Тогда
		ОбщегоНазначения.СообщитьПользователю(АвторизационныеДанные.detail);
		УдалитьИзВременногоХранилища(ДополнительныеПараметры.АдресХранилища);
		Возврат МассивОтветов;
	КонецЕсли;

	Для каждого ДанныеДокумента Из МассивДокументов Цикл

		ИмяВременногоФайлаPDF = ПолучитьИмяВременногоФайла(".pdf");
		Base64Значение(ДанныеДокумента.файлBase64).Записать(ИмяВременногоФайлаPDF);
		АрхивBase64 = Base64Строка(Новый ДвоичныеДанные(ИмяВременногоФайлаPDF));
		УдалитьФайлы(ИмяВременногоФайлаPDF);

		СтруктураДокументаДляОтправки = Новый Структура;

		СтруктураДокументаДляОтправки.Вставить("signed_xml", ДанныеДокумента.xml);
		СтруктураДокументаДляОтправки.Вставить("doc_file", Новый Структура("file_name, fileBase64", ДанныеДокумента.СтруктураДокументаМетаданные.source_guid + ".pdf", АрхивBase64));
		СтруктураДокументаДляОтправки.Вставить("doc", ДанныеДокумента.СтруктураДокументаМетаданные);

		АдресРесурса = "/api/v2/random_docs_out/";

		ДополнительныеПараметрыЗапроса = Новый Структура;
		ДополнительныеПараметрыЗапроса.Вставить("Прокси", Настройки.Прокси);

		Аутентификация = Новый Структура("Токен, Тип", АвторизационныеДанные.access_token, "Bearer");
		ДополнительныеПараметрыЗапроса.Вставить("Аутентификация", Аутентификация);

		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		Заголовки.Вставить("accept", "application/json");
		ДополнительныеПараметрыЗапроса.Вставить("Заголовки", Заголовки);

		// преобразование тела в json
		ПараметрыПреобразования = Новый Структура("ВариантЗаписиДатыJSON", ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением);
		ДополнительныеПараметрыЗапроса.Вставить("ПараметрыПреобразования", ПараметрыПреобразования);
		// для ответа в структуру
		ПараметрыПреобразованияJSON = Новый Структура("ПрочитатьВСоответствие", Ложь);
		ДополнительныеПараметрыЗапроса.Вставить("ПараметрыПреобразованияJSON", ПараметрыПреобразованияJSON);

		Результат = КоннекторHTTP.PostJson(Настройки.АдресСервера + АдресРесурса, СтруктураДокументаДляОтправки, ДополнительныеПараметрыЗапроса);	//

		Результат.Вставить("Документ", ДанныеДокумента.Документ);
		МассивОтветов.Добавить(Результат);

		Если Результат.Свойство("code") Тогда
			Если Результат["code"] = КодыСостоянияОтветаHTTPКлиентСервер.OK_200() Тогда
				СохранитьНомерДокумента(Результат["doc_id"], ДанныеДокумента.Документ);
			КонецЕсли;
		КонецЕсли;
		Если Результат.Свойство("doc_name") Тогда
			СохранитьНомерДокумента(Результат["doc_name"], ДанныеДокумента.Документ);
		КонецЕсли;
		Если Результат.Свойство("id") Тогда
			СохранитьИдентификаторДокумента(Результат["id"], ДанныеДокумента.Документ);
		КонецЕсли;

	КонецЦикла;
	
	Возврат МассивОтветов;

КонецФункции

// Получить http ссылку на документ.
//
// Параметры:
//	ДанныеДокумента - Структура - данные документа для получения ссылки
//	Отказ - Булево
//
// Возвращаемое значение:
//	Неопределено, Строка - http ссылка на документ
Функция ПолучитьHTTPСсылкуНаДокумент(ДанныеДокумента, Отказ) Экспорт

	Если ЗначениеЗаполнено(ДанныеДокумента.ИдентификаторДокумента) Тогда
		ИдДокумента = Формат(ДанныеДокумента.ИдентификаторДокумента, "ЧРГ=; ЧГ=");
	Иначе
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;

	Настройки = ПолучитьНастройкиПодключения(ДанныеДокумента.Организация);
	АдресРесурса = "/random_docs/";

	HTTPСсылкаНаДокумент = Настройки.АдресСервера + АдресРесурса + ИдДокумента;

	Возврат HTTPСсылкаНаДокумент;

КонецФункции

// Сформировать произвольные документы.
//
// Параметры:
//	ОтзываемыеДокументы - Массив из Структура - Отзываемые документы
//	АдресХранилища - УникальныйИдентификатор -	адрес во временном хранилище
//	Отказ - Булево - Признак отказа
Процедура СформироватьОтзываемыеДокументы(ОтзываемыеДокументы, АдресХранилища, Отказ) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ДанныеДокумента Из ОтзываемыеДокументы Цикл
		ДанныеДокумента.Вставить("Хэш", СтрШаблон("<root><md5-hash>%1</md5-hash></root>", ДанныеДокумента.НомерЭДО));
	КонецЦикла;

	ПоместитьВоВременноеХранилище(ОтзываемыеДокументы, АдресХранилища);

КонецПроцедуры

// Отзывает документ в Учет ЭДО.
//
// Параметры:
//   ОтзываемыеДокументы - Массив из Структура - Содержит данные документа, который необходимо отозвать
//   Отказ - Булево - Флаг отказа от отзыва документа
Процедура ОтозватьДокументы(ОтзываемыеДокументы, Отказ) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ДанныеДокумента Из ОтзываемыеДокументы Цикл

		Если ЗначениеЗаполнено(ДанныеДокумента.ИдентификаторДокумента) Тогда
			ИдДокумента = Формат(ДанныеДокумента.ИдентификаторДокумента, "ЧРГ=; ЧГ=");
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		МассивОтветов = Новый Массив;

		Настройки = ПолучитьНастройкиПодключения(ДанныеДокумента.Организация);
		АвторизационныеДанные = ПолучитьАвторизационныеДанные(Настройки);

		Если АвторизационныеДанные.Свойство("detail") Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(АвторизационныеДанные.detail);
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		АдресРесурса = "/api/v2/reject_request_out/";

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Прокси", Настройки.Прокси);

		Аутентификация = Новый Структура("Токен, Тип", АвторизационныеДанные.access_token, "Bearer");
		ДополнительныеПараметры.Вставить("Аутентификация", Аутентификация);

		Заголовки = Новый Соответствие;
		Заголовки.Вставить("accept", "application/json");
		ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

		ПараметрыПреобразованияJSON = Новый Структура("ПрочитатьВСоответствие", Ложь);
		ДополнительныеПараметры.Вставить("ПараметрыПреобразованияJSON", ПараметрыПреобразованияJSON);

		СтруктураДокументаДляОтправки = Новый Структура;

		СтруктураДокументаДляОтправки.Вставить("doc_id", ИдДокумента);
		ТипПроизвольныйДокумент = 5;
		СтруктураДокументаДляОтправки.Вставить("doc_type", ТипПроизвольныйДокумент);
		СтруктураДокументаДляОтправки.Вставить("comment", ДанныеДокумента.ПричинаОтзыва);
		СтруктураДокументаДляОтправки.Вставить("signed_xml", ДанныеДокумента.xml);

		Результат = КоннекторHTTP.PostJson(Настройки.АдресСервера + АдресРесурса, СтруктураДокументаДляОтправки, ДополнительныеПараметры);

		Результат.Вставить("Документ", ДанныеДокумента.Документ);
		МассивОтветов.Добавить(Результат);

		Если Результат.Свойство("detail") Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		Если Результат.Свойство("status") Тогда
			Статус_УчетЭДО = Статус_УчетЭДО(Результат.status);
			УправлениеСвойствами.ЗаписатьЗначениеСвойства(ДанныеДокумента.Документ, ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.Статус_УчетЭДО, Статус_УчетЭДО, Истина);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Обновить статусы докуменов.
//
// Параметры:
//	МассивДокументов - Массив из Структура - данные документа для получения ссылки
//	Отказ - Булево
Процедура ОбновитьСтатусыДокументов(МассивДокументов, Отказ) Экспорт

	ОрганизацияПоУмолчанию = Новый Структура("Организация", Справочники.Организации.ПустаяСсылка());
	Настройки = ПолучитьНастройкиПодключения(УчетЭДООбщегоНазначенияКлиентСервер.ПервыйВКоллекции(МассивДокументов, ОрганизацияПоУмолчанию).Организация);
	АвторизационныеДанные = ПолучитьАвторизационныеДанные(Настройки);

	Если АвторизационныеДанные.Свойство("detail") Тогда
		ОбщегоНазначения.СообщитьПользователю(АвторизационныеДанные.detail);
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	МассивОтветов = Новый Массив;

	Для Каждого ДанныеДокумента Из МассивДокументов Цикл

		АдресРесурса = "/api/v2/random_docs_out/" + Формат(ДанныеДокумента.ИдентификаторВЭДО, "ЧГ=");

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Прокси", Настройки.Прокси);

		Аутентификация = Новый Структура("Токен, Тип", АвторизационныеДанные.access_token, "Bearer");
		ДополнительныеПараметры.Вставить("Аутентификация", Аутентификация);

		Заголовки = Новый Соответствие;
		Заголовки.Вставить("accept", "application/json");
		ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

		// для ответа в структуру
		ПараметрыПреобразованияJSON = Новый Структура("ПрочитатьВСоответствие", Ложь);
		ДополнительныеПараметры.Вставить("ПараметрыПреобразованияJSON", ПараметрыПреобразованияJSON);

		Результат = КоннекторHTTP.GetJson(Настройки.АдресСервера + АдресРесурса, , ДополнительныеПараметры);

		Результат.Вставить("Документ", ДанныеДокумента.Документ);
		МассивОтветов.Добавить(Результат);

		Если Результат.Свойство("detail") Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		Если Результат.Свойство("status") Тогда
			Статус_УчетЭДО = Статус_УчетЭДО(Результат.status);
			ЗаписатьЗначениеСвойства(ДанныеДокумента.Документ, "Статус_УчетЭДО", Статус_УчетЭДО);
		КонецЕсли;

	КонецЦикла;

	//УведомленияКлиента.ОтправитьУведомление("ОтправленыДокументыВУчетЭДО", МассивОтветов);

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция РезультатПодготовкиОтправкиДокументов(ВыбранныеДокументы, Отбор = Неопределено) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("КоличествоВыбранных",	ВыбранныеДокументы.Количество());
	Результат.Вставить("КоличествоКОтправке",	0);
	Результат.Вставить("НеотправляемыеДокументы", Новый Массив);
	Результат.Вставить("ОтправляемыеДокументы",	Новый Массив);
	Результат.Вставить("ОтзываемыеДокументы",	Новый Массив);

	Если Отбор = Неопределено Тогда
		Отбор = Новый Структура;
	КонецЕсли;

	РазрешенаПовторнаяОтправка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Отбор, "РазрешенаПовторнаяОтправка", Ложь);

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Организация.ИНН КАК ИННОрганизации,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Контрагент.ИНН КАК ИННКонтрагента,
	|	РеализацияТоваровУслуг.Валюта.Наименование КАК ВалютаНаименование,
	|	ЕСТЬNULL(КонтрагентыКонтактнаяИнформация.АдресЭП, """") КАК АдресЭП,
	|	ЕСТЬNULL(СтатусЭДО.Значение, """") КАК СтатусЭДО,
	|	ЕСТЬNULL(НомерЭДО.Значение, """") КАК НомерЭДО,
	|	ЕСТЬNULL(ИдДокументаЭДО.Значение, 0) КАК ИдентификаторДокумента,
	|	РеализацияТоваровУслуг.Ссылка КАК Документ
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК СтатусЭДО
	|		ПО РеализацияТоваровУслуг.Ссылка = СтатусЭДО.Объект
	|		И СтатусЭДО.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Статус_УчетЭДО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|		ПО РеализацияТоваровУслуг.Контрагент = КонтрагентыКонтактнаяИнформация.Ссылка
	|		И КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	|		И КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтрагентаДляЭДО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК НомерЭДО
	|		ПО РеализацияТоваровУслуг.Ссылка = НомерЭДО.Объект
	|		И НомерЭДО.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Номер_УчетЭДО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ИдДокументаЭДО
	|		ПО РеализацияТоваровУслуг.Ссылка = ИдДокументаЭДО.Объект
	|		И ИдДокументаЭДО.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.id_УчетЭДО)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&Ссылка)";

	Запрос.УстановитьПараметр("Ссылка", ВыбранныеДокументы);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл

		Если Не ПустаяСтрока(Выборка.СтатусЭДО) И НЕ РазрешенаПовторнаяОтправка Тогда

			ДанныеДокумента = ДанныеНеотправляемогоДокумента();
			ДанныеДокумента.Документ = Выборка.Документ;
			ДанныеДокумента.Причина	= НСтр("ru = 'Документ уже передан в сервис Учет.ЭДО'");
			Результат.НеотправляемыеДокументы.Добавить(ДанныеДокумента);
			Продолжить;

		КонецЕсли;

		Если РазрешенаПовторнаяОтправка И СтатусыДляОтзыва_УчетЭДО().Найти(Выборка.СтатусЭДО) <> Неопределено
			И Выборка.НомерЭДО <> "" И Выборка.ИдентификаторДокумента <> 0 Тогда

			ДанныеДокумента = ДанныеОтзываемогоДокумента();
			ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
			ДанныеДокумента.ПричинаОтзыва = НСтр("ru = 'Ошибка в данных документа'");
			Результат.ОтзываемыеДокументы.Добавить(ДанныеДокумента);

		КонецЕсли;

		Если ПустаяСтрока(Выборка.АдресЭП) Тогда
			ДанныеДокумента = ДанныеНеотправляемогоДокумента();
			ДанныеДокумента.Документ = Выборка.Документ;
			ДанныеДокумента.Причина	= НСтр("ru = 'У контрагента не заполнен адрес электронной почты для ЭДО'");
			Результат.НеотправляемыеДокументы.Добавить(ДанныеДокумента);
			Продолжить;

		КонецЕсли;

		Если ПустаяСтрока(Выборка.ИННКонтрагента) Тогда
			ДанныеДокумента = ДанныеНеотправляемогоДокумента();
			ДанныеДокумента.Документ = Выборка.Документ;
			ДанныеДокумента.Причина	= НСтр("ru = 'Не заполнен ИИН/БИН контрагента'");
			Результат.НеотправляемыеДокументы.Добавить(ДанныеДокумента);
			Продолжить;

		КонецЕсли;

		ДлинаКорректногоБин = 12;
		Если СтрДлина(Выборка.ИННКонтрагента) <> ДлинаКорректногоБин Тогда
			ДанныеДокумента = ДанныеНеотправляемогоДокумента();
			ДанныеДокумента.Документ = Выборка.Документ;
			ДанныеДокумента.Причина	= СтрШаблон(НСтр("ru = 'Не верно заполнен ИИН/БИН, длина должна быть %1 символов'"), ДлинаКорректногоБин);
			Результат.НеотправляемыеДокументы.Добавить(ДанныеДокумента);

			Продолжить;

		КонецЕсли;

		ДанныеДокумента = НовыйДанныеДокумента();
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
		Результат.ОтправляемыеДокументы.Добавить(ДанныеДокумента);
		Результат.КоличествоКОтправке = Результат.КоличествоКОтправке + 1;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура ЗаписатьЗначениеСвойства(ВладелецСвойств, ИмяСвойства, Значение) Экспорт

	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ИмяСвойства);
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("Свойство");
	Таблицасвойств.Колонки.Добавить("Значение");

	НоваяСтрока = ТаблицаСвойств.Добавить();
	НоваяСтрока.Свойство = Свойство;
	НоваяСтрока.Значение = Значение;

	Для Каждого ТекВладелецСвойств Из ?(ТипЗнч(ВладелецСвойств) = Тип("Массив"), ВладелецСвойств, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВладелецСвойств)) Цикл
		УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ТекВладелецСвойств, ТаблицаСвойств);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция АдресСервера(ПараметрыПодключения)

	ПортЗапроса = "";
	СтандартныйПортHttp = 80;
	Если ЗначениеЗаполнено(ПараметрыПодключения.Порт) И ПараметрыПодключения.Порт <> СтандартныйПортHttp Тогда
		ПортЗапроса = СтрШаблон(":%1", XMLСтрока(ПараметрыПодключения.Порт));
	КонецЕсли;
	АдресСервера = СтрШаблон("%1://%2%3", ПротоколПодключения(ПараметрыПодключения.ЗащищенноеСоединение), ПараметрыПодключения.Сервер, ПортЗапроса);

	Возврат АдресСервера;

КонецФункции

Функция ПроксиПодключения(ПараметрыПодключения)

	Если НЕ ПустаяСтрока(ПараметрыПодключения.ПроксиСервер) Тогда
		Прокси = Новый ИнтернетПрокси(Ложь);
		Прокси.Установить("http", ПараметрыПодключения.ПроксиСервер, ПараметрыПодключения.ПортПроксиСервера, ПараметрыПодключения.ЛогинПрокси, ПараметрыПодключения.ПарольПрокси);
	Иначе
		Прокси = Неопределено;
	КонецЕсли;

	Возврат Прокси;

КонецФункции

Функция ПротоколПодключения(ЗащищенноеСоединение)

	Протокол = ?(ЗащищенноеСоединение, "https", "http");
	Возврат Протокол;

КонецФункции

Функция ДанныеНеотправляемогоДокумента()

	Данные = Новый Структура;
	Данные.Вставить("Документ");
	Данные.Вставить("Причина");

	Возврат Данные;

КонецФункции

Функция НовыйДанныеДокумента()

	Результат = Новый Структура;
	Результат.Вставить("Номер");
	Результат.Вставить("Дата");
	Результат.Вставить("Организация");
	Результат.Вставить("Контрагент");
	Результат.Вставить("Документ");
	Результат.Вставить("АдресЭП");
	Результат.Вставить("ИННКонтрагента");
	Результат.Вставить("ВалютаНаименование");
	Результат.Вставить("ИННОрганизации");

	Возврат Результат;

КонецФункции

Функция ДанныеОтзываемогоДокумента()

	Данные = Новый Структура;
	Данные.Вставить("Документ");
	Данные.Вставить("Организация");
	Данные.Вставить("ИдентификаторДокумента");
	Данные.Вставить("НомерЭДО");
	Данные.Вставить("ПричинаОтзыва");

	Возврат Данные;

КонецФункции

Функция ТаблицаПечати()

	КоличествоСимволов = 150;

	ТаблицаПечати = Новый ТаблицаЗначений;
	ТаблицаПечати.Колонки.Добавить("МенеджерПечати", ОбщегоНазначения.ОписаниеТипаСтрока(КоличествоСимволов));
	ТаблицаПечати.Колонки.Добавить("Макет", ОбщегоНазначения.ОписаниеТипаСтрока(КоличествоСимволов));

	НоваяСтрока = ТаблицаПечати.Добавить();
	НоваяСтрока.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	НоваяСтрока.Макет = "НакладнаяНаОтпускЗапасовНаСторону";

	НоваяСтрока = ТаблицаПечати.Добавить();
	НоваяСтрока.МенеджерПечати = "Обработка.ПечатьАктОбОказанииУслуг";
	НоваяСтрока.Макет = "Р1";

	Возврат ТаблицаПечати;

КонецФункции

Процедура СохранитьНомерДокумента(НомерЭД, ОтправляемыйДокумент)

	ЗаписатьЗначениеСвойства(ОтправляемыйДокумент, "Номер_УчетЭДО", НомерЭД);
	ЗаписатьЗначениеСвойства(ОтправляемыйДокумент, "УчетЭДО", НСтр("ru = 'Ожидает подписи'"));

КонецПроцедуры

Процедура СохранитьИдентификаторДокумента(ИдентификаторДокумента, ОтправляемыйДокумент)

	ЗаписатьЗначениеСвойства(ОтправляемыйДокумент, "id_УчетЭДО", ИдентификаторДокумента);

КонецПроцедуры

Функция Статус_УчетЭДО(Статус)

	Соответствие = Новый Соответствие;
	Соответствие.Вставить("3", "Ожидает подписи");
	Соответствие.Вставить("4", "Подписан");
	Соответствие.Вставить("5", "Отклонен");
	Соответствие.Вставить("6", "Отзыв на рассмотрении");
	Соответствие.Вставить("7", "Отозван");
	Соответствие.Вставить("8", "Отозван отправителем");

	Возврат Соответствие[Статус];

КонецФункции

Функция СтатусыДляОтзыва_УчетЭДО()

	СтатусыДляОтзыва = Новый Массив;
	СтатусыДляОтзыва.Добавить("Ожидает подписи");
																																																										   
	Возврат СтатусыДляОтзыва;

КонецФункции

Функция ХешСуммаДвоичныхДанных(ДвоичныеДанныеФайла, ПреобразоватьВHEX = Ложь, ПеревестиВНижнийРегистр = Ложь) Экспорт

	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(ДвоичныеДанныеФайла);

	ХешСумма = ХешированиеДанных.ХешСумма;

	Если ПреобразоватьВHEX Тогда
		ХешСумма = ПолучитьHexСтрокуИзДвоичныхДанных(ХешСумма);
	КонецЕсли;

	Если ПеревестиВНижнийРегистр Тогда
		ХешСумма = НРег(СтрЗаменить(ХешСумма, " ", ""));
	КонецЕсли;

	Возврат ХешСумма;

КонецФункции

#КонецОбласти
