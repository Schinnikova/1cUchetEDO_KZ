
#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для программной работы со свойствами

// Добавляет дополнительное свойство к переданному объекту.
//
// Параметры:
//  Владелец - ОбъектМетаданных
//           - Строка - полное имя объекта метаданных или имя набора свойств.
//           - СправочникСсылка.НаборыДополнительныхРеквизитовИСведений - ссылка на набор свойств.
//  Параметры - см. ПараметрыДобавленияСвойства.
//  ЭтоСведение - Булево - если Истина, будет добавлено дополнительное сведение.
//                         Значение по умолчанию Ложь - добавляется дополнительный реквизит.
//
Процедура ДобавитьСвойство(Владелец, Параметры, ЭтоСведение = Ложь) Экспорт
	
	Если ТипЗнч(Владелец) = Тип("ОбъектМетаданных") Тогда
		ИмяПредопределенного = СтрЗаменить(Владелец.ПолноеИмя(), ".", "_");
	ИначеЕсли ТипЗнч(Владелец) = Тип("Строка") Тогда
		Если СтрНайти(Владелец, ".") = 0 Тогда
			// Это набор свойств.
			ИмяПредопределенного = Владелец;
		Иначе
			ИмяПредопределенного = СтрЗаменить(Владелец, ".", "_");
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.НаборыДополнительныхРеквизитовИСведений") Тогда
		НаборСвойств = Владелец;
	Иначе
		НаборСвойств = НаборСвойствПоИмени(ИмяПредопределенного);
		Если НаборСвойств = Неопределено Тогда
			Попытка
				НаборСвойств = Справочники.НаборыДополнительныхРеквизитовИСведений[ИмяПредопределенного];
			Исключение
				// Такого предопределенного элемента не существует.
				НаборСвойств = Неопределено;
			КонецПопытки;
		КонецЕсли;
		Если НаборСвойств = Неопределено Тогда
			ТекстИсключения = НСтр("ru = 'Переданный объект ""%1"" не подключен к подсистеме Свойства.'");
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстИсключения, Владелец);
		КонецЕсли;
	КонецЕсли;
	
	ОтсутствующиеПараметры = Новый Массив;
	Если Не Параметры.Свойство("Наименование")
		Или Не ЗначениеЗаполнено(Параметры.Наименование) Тогда
		ОтсутствующиеПараметры.Добавить("Наименование");
	КонецЕсли;
	
	Если Не Параметры.Свойство("ТипЗначения")
	 Или Не ЗначениеЗаполнено(Параметры.ТипЗначения) Тогда
		Если Параметры.Свойство("Тип") И ЗначениеЗаполнено(Параметры.Тип) Тогда
			Параметры.ТипЗначения = Параметры.Тип;
		Иначе
			ОтсутствующиеПараметры.Добавить("ТипЗначения");
		КонецЕсли;
	КонецЕсли;
	
	Если ОтсутствующиеПараметры.Количество() > 0 Тогда
		ОтсутствующиеПараметры = СтрСоединить(ОтсутствующиеПараметры, ", ");
		ТекстИсключения = НСтр("ru = 'Не переданы обязательные параметры:
			|%1.'");
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстИсключения, ОтсутствующиеПараметры);
	КонецЕсли;
	
	ПустаяСсылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
	// 1. Проверка, что наименование используется.
	Результат = НаименованиеУжеИспользуется(ПустаяСсылка, НаборСвойств, Параметры.Наименование);
	Если ЗначениеЗаполнено(Результат) Тогда
		ВызватьИсключение Результат;
	КонецЕсли;
	
	// 2. Проверка, что имя уже используется.
	Если Параметры.Свойство("Имя")
		И ЗначениеЗаполнено(Параметры.Имя) Тогда
		Результат = ИмяУжеИспользуется(Параметры.Имя, ПустаяСсылка);
		Если ЗначениеЗаполнено(Результат) Тогда
			ВызватьИсключение Результат;
		КонецЕсли;
	Иначе
		Имя = "";
		ЗаголовокОбъекта = Параметры.Наименование;
		УправлениеСвойствамиСлужебный.УдалитьНедопустимыеСимволы(ЗаголовокОбъекта);
		ЗаголовокОбъектаЧастями = СтрРазделить(ЗаголовокОбъекта, " ", Ложь);
		Для Каждого ЧастьЗаголовка Из ЗаголовокОбъектаЧастями Цикл
			Имя = Имя + ВРег(Лев(ЧастьЗаголовка, 1)) + Сред(ЧастьЗаголовка, 2);
		КонецЦикла;
		
		Если УправлениеСвойствамиСлужебный.ИмяНачинаетсяСЦифры(Имя) Тогда
			Имя = "_" + Имя;
		КонецЕсли;
		
		Результат = ИмяУжеИспользуется(Имя, ПустаяСсылка);
		Если ЗначениеЗаполнено(Результат) Тогда
			УИД = Новый УникальныйИдентификатор();
			СтрокаУИД = СтрЗаменить(Строка(УИД), "-", "");
			Имя = Имя + "_" + СтрокаУИД;
		КонецЕсли;
		
		Параметры.Вставить("Имя", Имя);
	КонецЕсли;
	
	// 3. Проверка, что идентификатор для формул уже используется.
	Если Параметры.Свойство("ИдентификаторДляФормул")
		И ЗначениеЗаполнено(Параметры.ИдентификаторДляФормул) Тогда
		Результат = ИдентификаторДляФормулУжеИспользуется(Параметры.ИдентификаторДляФормул, ПустаяСсылка);
		Если ЗначениеЗаполнено(Результат) Тогда
			ВызватьИсключение Результат;
		КонецЕсли;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.НаборыДополнительныхРеквизитовИСведений");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", НаборСвойств);
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		
		НовоеСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НовоеСвойство, Параметры);
		
		НовоеСвойство.Заголовок                 = Параметры.Наименование;
		НовоеСвойство.ЭтоДополнительноеСведение = ЭтоСведение;
		НовоеСвойство.НаборСвойств              = НаборСвойств;
		
		// Для мультиязычной.
		НовоеСвойство.ЗаголовокЯзык1 = Параметры.Наименование;
		НовоеСвойство.ЗаголовокЯзык2 = Параметры.Наименование;
	
		НовоеСвойство.Записать();
		
		ОбъектНаборСвойств = НаборСвойств.ПолучитьОбъект();
		Если НовоеСвойство.ЭтоДополнительноеСведение Тогда
			ТабличнаяЧасть = ОбъектНаборСвойств.ДополнительныеСведения;
		Иначе
			ТабличнаяЧасть = ОбъектНаборСвойств.ДополнительныеРеквизиты;
		КонецЕсли;
		НайденнаяСтрока = ТабличнаяЧасть.Найти(НаборСвойств.Ссылка, "Свойство");
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ТабличнаяЧасть.Добавить();
			НоваяСтрока.Свойство = НовоеСвойство.Ссылка;
			ОбъектНаборСвойств.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Добавляет доступное значение для реквизита с типом ЗначенияСвойствОбъектов
// или ЗначенияСвойствОбъектовИерархия.
//
// Параметры:
//  Владелец  - Строка - имя дополнительного реквизита.
//            - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - дополнительный реквизит, 
//                для которого надо добавить значение.
//  Параметры - см. ПараметрыДобавленияЗначенияСвойства.
//  Иерархия  - Булево  
//
// Возвращаемое значение:
//  СправочникСсылка.ЗначенияСвойствОбъектов
//  СправочникСсылка.ЗначенияСвойствОбъектовИерархия
//
Функция ДобавитьЗначениеСвойства(Знач Владелец, Параметры, Иерархия = Ложь) Экспорт
	
	Если Не Параметры.Свойство("Наименование")
		Или Не ЗначениеЗаполнено(Параметры.Наименование) Тогда
		ВызватьИсключение НСтр("ru = 'Не указан обязательный параметр ""Наименование"".'");
	КонецЕсли;
	
	Если ТипЗнч(Владелец) = Тип("Строка") Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Имя", Владелец);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
			|ИЗ
			|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
			|ГДЕ
			|	ДополнительныеРеквизитыИСведения.Имя = &Имя";
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() = 0 Тогда
			ШаблонИсключения = НСтр("ru = 'Не найдено дополнительного реквизита с именем ""%1"".'");
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИсключения, Владелец);
		КонецЕсли;
		
		Владелец = Результат[0].Ссылка;
	КонецЕсли;
	
	ТипРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ТипЗначения");
	Если Не УправлениеСвойствамиСлужебный.ТипЗначенияСодержитЗначенияСвойств(ТипРеквизита) Тогда
		ШаблонИсключения = НСтр("ru = 'Добавление значений поддерживается только для дополнительных реквизитов
			|с типами ""%1"" или ""%2"". Тип значения текущего реквизита - ""%3""'");
		ШаблонИсключения = СтрЗаменить(ШаблонИсключения, Символы.ПС, " ");
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИсключения,
			Тип("СправочникСсылка.ЗначенияСвойствОбъектов"),
			Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия"),
			ТипРеквизита);
	КонецЕсли;
	
	ЭтоГруппа = Параметры.Свойство("ЭтоГруппа") И Параметры.ЭтоГруппа;
	Если Иерархия Тогда
		Если ЭтоГруппа Тогда
			ЗначениеРеквизита = Справочники.ЗначенияСвойствОбъектовИерархия.СоздатьГруппу();
		Иначе
			ЗначениеРеквизита = Справочники.ЗначенияСвойствОбъектовИерархия.СоздатьЭлемент();
		КонецЕсли;
	Иначе
		Если ЭтоГруппа Тогда
			ЗначениеРеквизита = Справочники.ЗначенияСвойствОбъектов.СоздатьГруппу();
		Иначе
			ЗначениеРеквизита = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеРеквизита.Владелец = Владелец;
	ЗаполнитьЗначенияСвойств(ЗначениеРеквизита, Параметры);
	
	ЗначениеРеквизита.Записать();
	
	Возврат ЗначениеРеквизита.Ссылка;
	
КонецФункции

// Возвращает параметры, необходимые для для добавления свойства.
// 
// Возвращаемое значение:
//  Структура:
//     * Наименование - Строка - обязательно для заполнения.
//     * ТипЗначения  - ОписаниеТипов - обязательно для заполнения.
//     * Имя          - Строка
//     * Комментарий  - Строка
//     * ЗаголовокФормыЗначения       - Строка
//     * ЗаголовокФормыВыбораЗначения - Строка
//     * ИдентификаторДляФормул - Строка
//     * МногострочноеПолеВвода - Булево
//     * Подсказка              - Строка
//
Функция ПараметрыДобавленияСвойства() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("ТипЗначения");
	Параметры.Вставить("Имя", "");
	Параметры.Вставить("Комментарий", "");
	Параметры.Вставить("ЗаголовокФормыЗначения", "");
	Параметры.Вставить("ЗаголовокФормыВыбораЗначения", "");
	Параметры.Вставить("ИдентификаторДляФормул", "");
	Параметры.Вставить("МногострочноеПолеВвода", Ложь);
	Параметры.Вставить("Подсказка", "");
	Параметры.Вставить("Тип"); // Для обратной совместимости.
	
	Возврат Параметры;
	
КонецФункции

// Возвращает параметры, требуемые для добавления значения свойства.
// 
// Возвращаемое значение:
//  Структура:
//     * Наименование - Строка - обязательно для заполнения.
//     * ПолноеНаименование - Строка
//     * Родитель - СправочникСсылка.ЗначенияСвойствОбъектов
//                - СправочникСсылка.ЗначенияСвойствОбъектовИерархия
//     * ЭтоГруппа - Булево
//
Функция ПараметрыДобавленияЗначенияСвойства() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("ПолноеНаименование", "");
	Параметры.Вставить("Родитель");
	Параметры.Вставить("ЭтоГруппа", Ложь);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для нестандартной обработки дополнительных свойств.

// Возвращает ссылку на предопределенный набор свойств по имени набора.
// Предназначена для наборов, указанных в процедуре
// УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
//
// Параметры:
//  ИмяНабора - Строка - имя получаемого набора свойств.
//
// Возвращаемое значение:
//  СправочникСсылка.НаборыДополнительныхРеквизитовИСведений - ссылка на набор свойств.
//  Неопределено - если предопределенный набор не существует.
//
// Пример:
//  Ссылка = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Пользователи");
//
Функция НаборСвойствПоИмени(ИмяНабора) Экспорт
	ПредопределенныеНаборыСвойств = УправлениеСвойствамиПовтИсп.ПредопределенныеНаборыСвойств();
	Набор = ПредопределенныеНаборыСвойств.Получить(ИмяНабора); // см. Справочники.НаборыДополнительныхРеквизитовИСведений.СвойстваНабора
	Если Набор = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат Набор.Ссылка;
	КонецЕсли;
КонецФункции

Функция НаименованиеУжеИспользуется(Свойство, НаборСвойств, Наименование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СвойстваНабора.Свойство КАК Свойство,
		|	РеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК СвойстваНабора
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК РеквизитыИСведения
		|		ПО (РеквизитыИСведения.Ссылка = СвойстваНабора.Свойство)
		|ГДЕ
		|	РеквизитыИСведения.Заголовок = &Наименование
		|	И СвойстваНабора.Ссылка = &НаборСвойств
		|	И СвойстваНабора.Свойство <> &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СвойстваНабора.Свойство КАК Свойство,
		|	РеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК СвойстваНабора
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК РеквизитыИСведения
		|		ПО (РеквизитыИСведения.Ссылка = СвойстваНабора.Свойство)
		|ГДЕ
		|	РеквизитыИСведения.Заголовок = &Наименование
		|	И СвойстваНабора.Ссылка = &НаборСвойств
		|	И СвойстваНабора.Свойство <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",       Свойство);
	Запрос.УстановитьПараметр("НаборСвойств", НаборСвойств);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если НЕ Выборка.Следующий() Тогда
		Возврат "";
	КонецЕсли;
	
	Если Выборка.ЭтоДополнительноеСведение Тогда
		ТекстВопроса = НСтр("ru = 'Существует дополнительное сведение с наименованием
		                          |""%1"".'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Существует дополнительный реквизит с наименованием
		                          |""%1"".'");
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстВопроса + Символы.ПС + Символы.ПС
		                         + НСтр("ru = 'Рекомендуется использовать другое наименование,
		                         |иначе приложение может работать некорректно.'"),
		Наименование);
	
	Возврат ТекстВопроса;
	
КонецФункции

Функция ИмяУжеИспользуется(Знач Имя, Знач ТекущееСвойство) Экспорт
	
	НовоеИмя = Имя;
	УправлениеСвойствамиСлужебный.УдалитьНедопустимыеСимволы(НовоеИмя);
	Если НовоеИмя <> Имя
		Или ИмяНачинаетсяСЦифры(Имя)
		Или СтрРазделить(НовоеИмя, " ", Истина).Количество() > 1 Тогда
		
		ТекстВопроса = НСтр("ru = 'Имя (группа Для разработчиков) должно состоять из одного слова, начинаться с буквы и не содержать специальных символов, кроме ""_"".'");
		Возврат ТекстВопроса;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Свойства.ЭтоДополнительноеСведение
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
	|ГДЕ
	|	Свойства.Имя = &Имя
	|	И Свойства.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ТекущееСвойство);
	Запрос.УстановитьПараметр("Имя",    Имя);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если НЕ Выборка.Следующий() Тогда
		Возврат "";
	КонецЕсли;
	
	Если Выборка.ЭтоДополнительноеСведение Тогда
		ТекстВопроса = НСтр("ru = 'Существует дополнительное сведение с именем
		                          |""%1"".'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Существует дополнительный реквизит с именем
		                          |""%1"".'");
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстВопроса + Символы.ПС + Символы.ПС
		                         + НСтр("ru = 'Рекомендуется использовать другое имя,
		                         |иначе приложение может работать некорректно.
		                         |
		                         |Создать новое имя и продолжить запись?'"),
		Имя);
	
	Возврат ТекстВопроса;
	
КонецФункции 

Функция ИмяНачинаетсяСЦифры(ИмяРеквизита) Экспорт
	ПервыйСимвол = Лев(ИмяРеквизита, 1);
	Если СтрНайти("0123456789", ПервыйСимвол) > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

Функция ИдентификаторДляФормулУжеИспользуется(Знач ИдентификаторДляФормул, Знач ТекущееСвойство) Экспорт
	
	ПроверочныйИдентификатор = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ИдентификаторДляФормул(ИдентификаторДляФормул);
	Если ВРег(ИдентификаторДляФормул) <> ВРег(ПроверочныйИдентификатор) Тогда
		ТекстВопроса = НСтр("ru = 'Идентификатор ""%1"" не соответствует правилам именования переменных.
		                          |Идентификатор не должен содержать пробелов и специальных символов.'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстВопроса,
			ИдентификаторДляФормул);
		Возврат ТекстВопроса;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Свойства.ЭтоДополнительноеСведение
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
	|ГДЕ
	|	Свойства.ИдентификаторДляФормул = &ИдентификаторДляФормул
	|	И Свойства.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ТекущееСвойство);
	Запрос.УстановитьПараметр("ИдентификаторДляФормул", ИдентификаторДляФормул);
	
	НачатьТранзакцию();
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если НЕ Выборка.Следующий() Тогда
		Возврат "";
	КонецЕсли;
	
	Если Выборка.ЭтоДополнительноеСведение Тогда
		ТекстВопроса = НСтр("ru = 'Существует дополнительное сведение с идентификатором для формул
		                          |""%1"".'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Существует дополнительный реквизит с идентификатором для формул
		                          |""%1"".'");
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстВопроса + Символы.ПС + Символы.ПС
		                         + НСтр("ru = 'Рекомендуется использовать другой идентификатор для формул,
		                         |иначе приложение может работать некорректно.'"),
		ИдентификаторДляФормул);
	
	Возврат ТекстВопроса;
	
КонецФункции

Функция СоздатьДополнительныеСведения() Экспорт
	
	Успешно = Истина;
	ТаблицаДопСведений = Новый ТаблицаЗначений;
	Строка100 = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(100));
	ТаблицаДопСведений.Колонки.Добавить("Имя", Строка100);
	ТаблицаДопСведений.Колонки.Добавить("Наименование", Строка100);                                         
	ТаблицаДопСведений.Колонки.Добавить("ТипЗначения", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(25)));
	ТаблицаДопСведений.Колонки.Добавить("Длина", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4)));
	
	ЗаполнитьЗначенияСвойств(ТаблицаДопСведений.Добавить(), Новый Структура("Имя, Наименование, ТипЗначения, Длина", "Номер_УчетЭДО", "Номер Учет.ЭДО", "Строка", 36));
	ЗаполнитьЗначенияСвойств(ТаблицаДопСведений.Добавить(), Новый Структура("Имя, Наименование, ТипЗначения, Длина", "Статус_УчетЭДО", "Статус Учет.ЭДО", "Строка", 25));
	ЗаполнитьЗначенияСвойств(ТаблицаДопСведений.Добавить(), Новый Структура("Имя, Наименование, ТипЗначения, Длина", "id_УчетЭДО", "id Учет.ЭДО", "Число", 10));	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблРекв.Имя КАК Имя,
		|	ТаблРекв.Наименование КАК Наименование,
		|	ТаблРекв.ТипЗначения КАК ТипЗначения,
		|	ТаблРекв.Длина КАК Длина
		|ПОМЕСТИТЬ ВТ_ТаблРекв
		|ИЗ
		|	&ТаблРекв КАК ТаблРекв
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДополнительныеРеквизитыИСведения.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка)) КАК Ссылка,
		|	ВТ_ТаблРекв.Имя КАК Имя,
		|	ВТ_ТаблРекв.Наименование КАК Наименование,
		|	ВТ_ТаблРекв.ТипЗначения КАК ТипЗначения,
		|	ВТ_ТаблРекв.Длина КАК Длина
		|ИЗ
		|	ВТ_ТаблРекв КАК ВТ_ТаблРекв
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|		ПО ВТ_ТаблРекв.Имя = ДополнительныеРеквизитыИСведения.Имя
		|			И НЕ ДополнительныеРеквизитыИСведения.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТаблРекв", ТаблицаДопСведений); 
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка)Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			СоздатьДополнительноеСведение(ВыборкаДетальныеЗаписи.Имя, ВыборкаДетальныеЗаписи.Наименование, ВыборкаДетальныеЗаписи.ТипЗначения, ВыборкаДетальныеЗаписи.Длина);
		Исключение
			Успешно = Ложь;
			Прервать;
		КонецПопытки		
	КонецЦикла;          

	Возврат Успешно;

КонецФункции

Процедура СоздатьДополнительноеСведение(Имя, Наименование, ТипЗначения, Длина)
	
	УстановитьПривилегированныйРежим(Истина);

	Константы.ИспользоватьДополнительныеРеквизитыИСведения.Установить(Истина);
	
	Параметры = УчетЭДОУправлениеСвойствами.ПараметрыДобавленияСвойства();
	
	Параметры.Имя = Имя;
	Параметры.Наименование = Наименование;
	Параметры.ТипЗначения = Новый ОписаниеТипов(ТипЗначения, , , Новый КвалификаторыЧисла(Длина), Новый КвалификаторыСтроки(Длина));
	
	УчетЭДОУправлениеСвойствами.ДобавитьСвойство(Метаданные.Документы.РеализацияТоваровУслуг, Параметры, Истина);
	
КонецПроцедуры

#КонецОбласти
